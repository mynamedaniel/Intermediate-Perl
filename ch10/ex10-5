#!/usr/bin/perl
use warnings;
use strict;
use 5.014;
use Data::Dumper;
use File::Basename;
use File::Spec::Functions;

my $data = data_for_path("/", 'depth');
print Dumper($data);

sub data_for_path {
	my( $path, $type ) = @_;
	my $data = {};
	my @queue = ([$path, 0, $data]);
	while (my $next = shift @queue) {
		my($path, $level, $ref) = @$next;
		my $basename = basename($path);
		$ref->{$basename} = do {
			if (-f $path or -l $path) {undef}
			else {
				my $hash = {};
				opendir my $dh, $path;
				my @new_paths = map {
					catfile($path, $_)
				} grep {! /^\.\.?\z/} readdir $dh; 
				if ($type eq 'depth') {
					unshift @queue, map { [$_, $level + 1, $hash] } @new_paths;
				}
				else {
					push @queue, map { [$_, $level + 1, $hash] } @new_paths;
				}
				$hash;
			}
		};
	}
	$data;
}
